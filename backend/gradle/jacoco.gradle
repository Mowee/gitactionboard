apply plugin: 'jacoco'

def excludes = [
        "de/otto/platform/gitactionboard/adapters/service/notifications/NoOpsNotificationsConnector.class"
]

def jacocoExecutionDataFiles = fileTree(buildDir)
        .include("/jacoco/test.exec")
        .include("/jacoco/integrationTest.exec")

jacocoTestReport {
    reports {
        xml.required = false
        html.required = true
        csv.required = false
    }

    dependsOn integrationTest
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        executionData.from = files(jacocoExecutionDataFiles)
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.80
            }
        }
        rule {
            limit {
                counter = 'INSTRUCTION'
                minimum = 0.89
            }
        }
        rule {
            limit {
                counter = 'METHOD'
                minimum = 0.85
            }
        }
        rule {
            limit {
                counter = 'CLASS'
                minimum = 0.89
            }
        }
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: excludes)
        })
    }
}

integrationTest.finalizedBy jacocoTestCoverageVerification, mergeTestReports
